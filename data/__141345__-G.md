

#### Variable re-arrangement by storage packing

Reference: [Layout of State Variables in Storage](https://docs.soliditylang.org/en/v0.8.13/internals/layout_in_storage.html).

##### IDeployer.sol

In `struct DeploymentParams`, each `uint48` can be placed with a `uint192`, as a result, the 2 variables will be packed into 1 slot storage. Otherwise, the `uint192` could occupy 1 slot by itself. According to the currently layout, from line 24 to line 46, 7 `uint192` and 6 `uint48`, there are total 8 slots, but after rearrangement, they can be packed into 7 slots.

```solidity
File: contracts\interfaces\IDeployer.sol
19: struct DeploymentParams {
20:     // === Revenue sharing ===
21:     RevenueShare dist; // revenue sharing splits between RToken and RSR
22:     //
23:     // === Trade sizing ===
24:     uint192 minTradeVolume; // {UoA}
25:     uint192 rTokenMaxTradeVolume; // {UoA}
26:     //
27:     // === Freezing ===
28:     uint48 shortFreeze; // {s} how long an initial freeze lasts
29:     uint48 longFreeze; // {s} how long each freeze extension lasts
30:     //
31:     // === Rewards (Furnace + StRSR) ===
32:     uint192 rewardRatio; // the fraction of available revenues that stRSR holders get each PayPeriod
33:     uint48 rewardPeriod; // {s} the atomic unit of rewards, determines # of exponential rounds
34:     //
35:     // === StRSR ===
36:     uint48 unstakingDelay; // {s} the "thawing time" of staked RSR before withdrawal
37:     //
38:     // === BackingManager ===
39:     uint48 tradingDelay; // {s} how long to wait until starting auctions after switching basket
40:     uint48 auctionLength; // {s} the length of an auction
41:     uint192 backingBuffer; // {1} how much extra backing collateral to keep
42:     uint192 maxTradeSlippage; // {1} max slippage acceptable in a trade
43:     //
44:     // === RToken ===
45:     uint192 issuanceRate; // {1/block} number of RToken to issue per block / (RToken value)
46:     uint192 scalingRedemptionRate; // {1/hour} max fraction of supply that can be redeemed hourly
47:     uint256 redemptionRateFloor; // {qRTok/hour} the lowest possible hourly redemption limit
48: }
```

It can be rearranged to 
```diff
struct DeploymentParams {
    // === Revenue sharing ===
    RevenueShare dist; // revenue sharing splits between RToken and RSR
    //
    // === Trade sizing ===
    uint192 minTradeVolume; // {UoA}
+   uint48 rewardPeriod; // {s} the atomic unit of rewards, determines # of exponential rounds
    uint192 rTokenMaxTradeVolume; // {UoA}
    //
    // === Freezing ===
    uint48 shortFreeze; // {s} how long an initial freeze lasts
    uint48 longFreeze; // {s} how long each freeze extension lasts
    //
    // === Rewards (Furnace + StRSR) ===
    uint192 rewardRatio; // the fraction of available revenues that stRSR holders get each PayPeriod
-    uint48 rewardPeriod; // {s} the atomic unit of rewards, determines # of exponential rounds
    //
    // === StRSR ===
-    uint48 unstakingDelay; // {s} the "thawing time" of staked RSR before withdrawal
    //
    // === BackingManager ===
-    uint48 tradingDelay; // {s} how long to wait until starting auctions after switching basket
    uint48 auctionLength; // {s} the length of an auction
    uint192 backingBuffer; // {1} how much extra backing collateral to keep
    uint192 maxTradeSlippage; // {1} max slippage acceptable in a trade
+    uint48 unstakingDelay; // {s} the "thawing time" of staked RSR before withdrawal
    //
    // === RToken ===
    uint192 issuanceRate; // {1/block} number of RToken to issue per block / (RToken value)
+    uint48 tradingDelay; // {s} how long to wait until starting auctions after switching basket
    uint192 scalingRedemptionRate; // {1/hour} max fraction of supply that can be redeemed hourly
    uint256 redemptionRateFloor; // {qRTok/hour} the lowest possible hourly redemption limit
}
```

##### FiatCollateral.sol

In `struct CollateralConfig`, `uint48 priceTimeout` can be placed next to `uint192 oracleError`, as a result, 1 slot storage can be saved. According to the currently layout, they both occupy 1 slot, but after re-arrangement, they can be packed into 1 slot.


```solidity
File: contracts/plugins/assets/FiatCollateral.sol
11: struct CollateralConfig {
12:     uint48 priceTimeout; // {s} The number of seconds over which saved prices decay
13:     AggregatorV3Interface chainlinkFeed; // Feed units: {target/ref}
14:     uint192 oracleError; // {1} The % the oracle feed can be off by
15:     IERC20Metadata erc20; // The ERC20 of the collateral token
16:     uint192 maxTradeVolume; // {UoA} The max trade volume, in UoA
17:     uint48 oracleTimeout; // {s} The number of seconds until a oracle value becomes invalid
18:     bytes32 targetName; // The bytes32 representation of the target name
19:     uint192 defaultThreshold; // {1} A value like 0.05 that represents a deviation tolerance
20:     // set defaultThreshold to zero to create SelfReferentialCollateral
21:     uint48 delayUntilDefault; // {s} The number of seconds an oracle can mulfunction
22: }
```

can be changed to
```diff
struct CollateralConfig {
-    uint48 priceTimeout; // {s} The number of seconds over which saved prices decay
    AggregatorV3Interface chainlinkFeed; // Feed units: {target/ref}
+    uint48 priceTimeout; // {s} The number of seconds over which saved prices decay    
    uint192 oracleError; // {1} The % the oracle feed can be off by
    IERC20Metadata erc20; // The ERC20 of the collateral token
    uint192 maxTradeVolume; // {UoA} The max trade volume, in UoA
    uint48 oracleTimeout; // {s} The number of seconds until a oracle value becomes invalid
    bytes32 targetName; // The bytes32 representation of the target name
    uint192 defaultThreshold; // {1} A value like 0.05 that represents a deviation tolerance
    // set defaultThreshold to zero to create SelfReferentialCollateral
    uint48 delayUntilDefault; // {s} The number of seconds an oracle can mulfunction
}
```

#### `assets[erc20]` can be saved to local cache

Each storage read uses opcode `sload` which costs 100 gas (warm access), while memory read uses `mload` which only cost 3 gas. ([reference](https://ethereum.org/en/developers/docs/evm/opcodes/))

```solidity
File: contracts/p1/AssetRegistry.sol
165:             if (assets[erc20] == asset) return false;
166:             else emit AssetUnregistered(erc20, assets[erc20]);
```

Suggestion:
Save `assets[erc20]` to local memory variable to save 1 sload operation.


#### `asset.erc20()` can be saved to local variable 

Each `asset.erc20()` will incur function call of getter and 1 sload. Each storage read uses opcode `sload` which costs 100 gas (warm access), while memory read uses `mload` which only cost 3 gas. ([reference](https://ethereum.org/en/developers/docs/evm/opcodes/)).

```solidity
File: contracts/p1/AssetRegistry.sol
87:     function unregister(IAsset asset) external governance {
88:         require(_erc20s.contains(address(asset.erc20())), "no asset to unregister");
89:         require(assets[asset.erc20()] == asset, "asset not found");
90:         uint192 quantity = basketHandler.quantity(asset.erc20());
91: 
92:         _erc20s.remove(address(asset.erc20()));
93:         assets[asset.erc20()] = IAsset(address(0));
94:         emit AssetUnregistered(asset.erc20(), asset);
95: 
96:         if (quantity > 0) basketHandler.disableBasket();
97:     }
```

Suggestion:
Save `asset.erc20()` to local memory variable to save 6 sload operation.


#### Splitting `require()` statements that use &&

`require()` statements with multiple conditions can be split.

See this [issue](https://github.com/code-423n4/2022-01-xdefi-findings/issues/128) which describes the fact that there is a larger deployment gas cost, but with enough runtime calls, the change ends up being cheaper.

The demo of the gas comparison can be seen [here](https://github.com/141345/gas_demo/blob/main/multirequire.sol).

Instances number of this issue: 2
```solidity 
File: contracts/p1/Deployer.sol
48:         require(
49:             address(rsr_) != address(0) &&
50:                 address(gnosis_) != address(0) &&
51:                 address(rsrAsset_) != address(0) &&
52:                 address(implementations_.main) != address(0) &&
53:                 address(implementations_.trade) != address(0) &&
54:                 address(implementations_.components.assetRegistry) != address(0) &&
55:                 address(implementations_.components.backingManager) != address(0) &&
56:                 address(implementations_.components.basketHandler) != address(0) &&
57:                 address(implementations_.components.broker) != address(0) &&
58:                 address(implementations_.components.distributor) != address(0) &&
59:                 address(implementations_.components.furnace) != address(0) &&
60:                 address(implementations_.components.rsrTrader) != address(0) &&
61:                 address(implementations_.components.rTokenTrader) != address(0) &&
62:                 address(implementations_.components.rToken) != address(0) &&
63:                 address(implementations_.components.stRSR) != address(0),
64:             "invalid address"
65:         );


File: contracts/p1/RevenueTrader.sol
72:         require(buyPrice > 0 && buyPrice < FIX_MAX, "buy asset price unknown");
``` 

#### Deployment gas

Line 83-84 is duplication of the function `getPastEra()`. 
```solidity
File: contracts/p1/StRSRVotes.sol
82:     function getPastTotalSupply(uint256 blockNumber) public view returns (uint256) {
83:         require(blockNumber < block.number, "ERC20Votes: block not yet mined");
84:         uint256 pastEra = _checkpointsLookup(_eras, blockNumber);
85:         return _checkpointsLookup(_totalSupplyCheckpoints[pastEra], blockNumber);
86:     }
87: 
88:     function getPastEra(uint256 blockNumber) public view returns (uint256) {
89:         require(blockNumber < block.number, "ERC20Votes: block not yet mined");
90:         return _checkpointsLookup(_eras, blockNumber);
91:     }
```

Suggestion:
replace line 83-84 with `getPastEra()`.


#### Iterating in `totals()` 

Each time `totals()` is called, the whole `destinations` will be looped. Each iteration involves sload, which cost 100 gas. 

```solidity
File: contracts/p1/Distributor.sol
141:     function totals() public view returns (RevenueTotals memory revTotals) {
142:         uint256 length = destinations.length();
143:         for (uint256 i = 0; i < length; ++i) {
144:             RevenueShare storage share = distribution[destinations.at(i)];
145:             revTotals.rTokenTotal += share.rTokenDist;
146:             revTotals.rsrTotal += share.rsrDist;
147:         }
148:     }
```

Suggestion:
Save the total sum into some variable, and update it if the `destinations` is changed. Instead of duplicate looping.


#### Replace sload with memory variable


Each storage read uses opcode `sload` which costs 100 gas (warm access), while memory read uses `mload` which only cost 3 gas. ([reference](https://ethereum.org/en/developers/docs/evm/opcodes/))

`unstakingDelay` in line 816 can be replaced by `val`. Saving 1 sload.
```solidity
File: contracts/p1/StRSR.sol
815:         unstakingDelay = val;
816:         require(rewardPeriod * 2 <= unstakingDelay, "unstakingDelay/rewardPeriod incompatible");
```

`rewardPeriod` in line 824 can be replaced by `val`. Saving 1 sload.
```solidity
File: contracts/p1/StRSR.sol
823:         rewardPeriod = val;
824:         require(rewardPeriod * 2 <= unstakingDelay, "unstakingDelay/rewardPeriod incompatible");
```


